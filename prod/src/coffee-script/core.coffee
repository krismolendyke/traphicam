class trphcm
    constructor: ->
        # Create and connect a SocketIO socket.
        @socket = new io.Socket()
        @socket.connect()

        @socket.on 'connect', ->
            $('div[data-role="content"]').append $('<p>').text 'Connected'

        @socket.on 'disconnect', ->
            $('div[data-role="content"]').append $('<p>').text 'Disconnected'

        @socket.on 'message', (raw) =>
            msg = JSON.parse raw

            # A message from the server.
            if msg.message
                $('div[data-role="content"]')
                    .append $('<p>').text "#{msg.message.text}"

            # A result from the server based on current position.
            if msg.currentPosition then @load msg

        # Currently the only way to refresh position is with your thumb.
        $('a#refresh').click => @sendCurrentPosition()

        # OK, well, the first load sends your position if you agree...
        @sendCurrentPosition()

    # Load the camera list with images and information about each image.
    load: (msg) =>
        # Show sensible units for accuracy.  Feet if under a half mile, miles
        # if over.
        if msg.currentPosition.position.coords.accuracy > 2640
            accuracy = "#{(parseFloat(msg.currentPosition.position.coords.accuracy, 10) / 5280).toFixed 2} miles"
        else
            accuracy = "#{msg.currentPosition.position.coords.accuracy} feet"

        # When the current position was taken, as a `Date`
        currentPositionDate = new Date msg.currentPosition.position.timestamp

        # The time the current position was taken, as a `String` to display
        # near each camera image.
        currentPositionTimeString = currentPositionDate.toLocaleTimeString()

        # Update the statistics block.
        $('p#stats').empty().text """It looks like you're at
(#{msg.currentPosition.position.coords.latitude}, #{msg.currentPosition.position.coords.longitude}), within an accuracy of #{accuracy} and that there's at least #{msg.currentPosition.cameras.results.length} cameras within a distance of #{msg.currentPosition.cameras.maxDistance} miles at #{currentPositionDate}."""

        # Append each camera as an item in the camera list.
        for camera in msg.currentPosition.cameras.results
            # TODO: Do this img anti-cache server-side with node's URL API.
            if camera.obj.url.indexOf '?' is -1
                queryString = "?t=#{Date.now()}"
            else
                queryString = "&t=#{Date.now()}"
            camera.obj.url = "#{camera.obj.url}#{queryString}"

            # Inject the time string so it can be displayed near the image.
            camera.time = currentPositionTimeString

            # Add the camera item generated by the template.
            $('ul#cam-list').append($('script#cam-item').tmpl(camera))

        # Currently, jQuery Mobile requires this call after updating a list
        # dynamically.
        $('ul#cam-list').listview 'refresh'

        # Spinner: stop!
        $.mobile.pageLoading true

    # Send the current position of the device if possible, otherwise let the
    # server know that this device doesn't support geolocation.
    sendCurrentPosition: =>
        if navigator.geolocation
            $.mobile.pageLoading()
            $('ul#cam-list').empty()
            navigator.geolocation.getCurrentPosition ((position) =>
                @socket.send JSON.stringify currentPosition:
                    position: position
                ), (error) => # TODO: Deal with geoloc error nicely.
        else
            @socket.send JSON.stringify error:
                message: 'Geolocation unsupported.'
